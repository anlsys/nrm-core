  λ(dataDir : Text)
-> λ(manifestDir : Text)
→ let lib = ./lib.dh dataDir manifestDir

  let message = "HelloWorld"

  let cn = "testContainer"

  let basicManifest = manifestDir ++ "perfwrap.dhall"

  let singularityManifest = manifestDir ++ "singularity.yml"

  let argIfTest =
          λ(isTest : Bool)
        → { isTest =
              isTest
          , timeout =
                    if isTest

              then  [ 600 ] : Optional Natural

              else  [] : Optional Natural
          }

  let Powercap = < Cap : Text | NoCap : {} >

  in  { run-singularity =
          lib.makeBase
          (   lib.emptyMakeBaseArg "echo" singularityManifest cn
            ⫽ { args =
                  [ message ]
              , verbose =
                  True
              , singularity =
                  True
              , cmdwants =
                  [] : List Text
              , cmdavoids =
                  [ "Traceback" ]
              , timeout =
                  [ 600 ] : Optional Natural
              }
          )
      , run =
          lib.makeBase
          (   lib.emptyMakeBaseArg "echo" basicManifest cn
            ⫽ { args =
                  [ message ]
              , verbose =
                  True
              , cmdwants =
                  [] : List Text
              , cmdavoids =
                  [ "Traceback" ]
              , timeout =
                  [ 600 ] : Optional Natural
              }
          )
      , pwd =
          lib.makeBase
          (   lib.emptyMakeBaseArg "pwd" basicManifest cn
            ⫽ { args =
                  [] : List Text
              , verbose =
                  True
              , cmdwants =
                  [ "toto" ] : List Text
              , cmdavoids =
                  [ "Traceback" ]
              , timeout =
                  [ 600 ] : Optional Natural
              , runwd =
                  [ "toto" ] : Optional Text
              }
          )
      , exitcode =
          lib.makeBase
          (   lib.emptyMakeBaseArg "false" basicManifest cn
            ⫽ { args =
                  [] : List Text
              , verbose =
                  True
              , cmdwants =
                  [] : List Text
              , cmdavoids =
                  [ "Traceback" ]
              , timeout =
                  [ 600 ] : Optional Natural
              , runExitcode =
                  [ +1 ] : Optional Integer
              }
          )
      , hello =
          lib.makeBase
          (   lib.emptyMakeBaseArg "echo" basicManifest cn
            ⫽ { args =
                  [ message ]
              , verbose =
                  True
              , cmdwants =
                  [ message ]
              , cmdavoids =
                  [ "Traceback" ]
              , timeout =
                  [ 600 ] : Optional Natural
              }
          )
      , listen =
          lib.makeExtended
          (   lib.emptyMakeBaseArg "sleep" basicManifest cn
            ⫽ { args =
                  [ "1" ]
              , verbose =
                  True
              , cmdwants =
                  [] : List Text
              , cmdavoids =
                  [ "Traceback" ]
              , timeout =
                  [ 600 ] : Optional Natural
              }
          )
          [   lib.values.emptyCmd
              { name =
                  "nrm"
              , outfile =
                  "nrmlisten.out"
              , errfile =
                  "nrmlisten.err"
              }
            ⫽ { args = [ "listen", "-u", cn ] }
          ]
      , power =
          lib.appTest
          (   lib.emptyFilterTestArgs "power" "sleep"
            ⫽ { timeout =
                  [ 600 ] : Optional Natural
              , args =
                  [ "15" ]
              , isTest =
                  True
              }
          )
      , performance =
          lib.appTest
          (   lib.emptyFilterTestArgs "performance" "sleep"
            ⫽ { timeout =
                  [ 600 ] : Optional Natural
              , args =
                  [ "15" ]
              , isTest =
                  True
              , manifestname =
                  "perfwrap.yml"
              }
          )
      , qmcpack =
            λ(isTest : Bool)
          → λ(powercap : Powercap)
          → lib.extendApp
            ( lib.progressAppTest
              (   lib.emptyProgressAppTestArg
                  "mpiexec"
                  [ "-n"
                  , if isTest then "2" else "24"
                  , "qmcpack"
                  , lib.qmcPackDir ++ "/simple-H2O.xml"
                  ]
                ⫽ argIfTest isTest
                ⫽ { cmdavoids =
                      [ "Error", "error", "ERROR" ]
                  , powercap =
                      powercap
                  }
              )
            )
            isTest
      , lammps =
            λ(isTest : Bool)
          → λ(powercap : Powercap)
          → lib.extendApp
            ( lib.progressAppTest
              (   lib.emptyProgressAppTestArg
                  "mpiexec"
                  [ "-n"
                  , if isTest then "2" else "24"
                  , "-bind-to"
                  , "core"
                  , "lmp_mpi"
                  , "-i"
                  , lib.lammpsDir ++ "/modified.lj"
                  ]
                ⫽ argIfTest isTest
                ⫽ { cmdavoids =
                      [ "Error", "error", "ERROR" ]
                  , powercap =
                      powercap
                  }
              )
            )
            isTest
      , openmc =
            λ(isTest : Bool)
          → λ(powercap : Powercap)
          → lib.extendApp
            (   lib.progressAppTest
                (   lib.emptyProgressAppTestArg
                    "mpiexec"
                    [ "-n", if isTest then "2" else "24", "openmc" ]
                  ⫽ argIfTest isTest
                  ⫽ { passvars =
                        [ "OPENMC_CROSS_SECTIONS" ]
                    , cmdavoids =
                        [ "Error" ]
                    , powercap =
                        powercap
                    }
                )
              ⫽ { pre =
                    [ "cp --no-preserve=mode -r " ++ lib.openmcDir ++ "/* ." ]
                }
            )
            isTest
      , amg =
            λ(isTest : Bool)
          → λ(powercap : Powercap)
          → lib.extendApp
            ( lib.progressAppTest
              (   lib.emptyProgressAppTestArg
                  "mpiexec"
                  [ "-n"
                  , if isTest then "2" else "24"
                  , "amg"
                  , "-problem"
                  , "2"
                  , "-n"
                  , "90"
                  , "90"
                  , "90"
                  , "-P"
                  , "2"
                  , if isTest then "1" else "12"
                  , "1"
                  ]
                ⫽ argIfTest isTest
                ⫽ { vars =
                      [ { varname = "OMP_NUM_THREADS", value = "1" } ]
                  , cmdavoids =
                      [ "Error", "error", "ERROR" ]
                  , powercap =
                      powercap
                  }
              )
            )
            isTest
      , stream =
            λ(isTest : Bool)
          → λ(powercap : Powercap)
          → lib.extendApp
            ( lib.progressAppTest
              (   lib.emptyProgressAppTestArg "stream_c" ([] : List Text)
                ⫽ argIfTest isTest
                ⫽ { vars =
                      [ { varname =
                            "OMP_NUM_THREADS"
                        , value =
                            if isTest then "2" else "24"
                        }
                      , { varname = "OMP_PLACES", value = "cores" }
                      ]
                  , cmdavoids =
                      [ "Error", "error", "ERROR" ]
                  , powercap =
                      powercap
                  }
              )
            )
            isTest
      }
