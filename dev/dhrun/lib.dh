  λ(dataDir : Text)
→ λ(manifestDir : Text)
→ let types = ./dhrun/types.dhall
let values = ./dhrun/values.dhall
  
  let assetDir = dataDir ++ "assets/"
  
  let openmcDir = assetDir ++ "openmc/"
  
  let lammpsDir = assetDir
  
  let qmcPackDir = assetDir
  
  let basicCommand =
          λ ( args
            : { name :
                  Text
              , outprefix :
                  Text
              , outwants :
                  List Text
              , outavoids :
                  List Text
              }
            )
        → { name =
              args.name
          , args =
              [] : List Text
          , out =
              { filename = "${args.outprefix}.out", filecheck = args.outavoids }
          , err =
              { filename = "${args.outprefix}.err", filecheck = args.outavoids }
          , postchecks =
                [ { filename =
                      "${args.outprefix}.out"
                  , filecheck =
                      { wants = args.outwants, avoids = args.outavoids }
                  }
                ]
              : List types.FileCheck
          , vars =
              [] : List types.EnvVar
          , passvars =
              [ "PATH", "PWD", "PYTHONPATH", "NRMSO" ]
          , timeout =
              [] : Optional Integer
          , otherwd =
              [] : Optional Text
          , exitcode =
              [] : Optional types.ExitCode
          }
  
  let Powercap = < Cap : Text | NoCap : {} >
  
  let MakeBaseArg =
        { manifest :
            Text
        , cmd :
            Text
        , daemonCfg :
            Text
        , containerName :
            Text
        , args :
            List Text
        , verbosity :
            types.Verbosity
        , singularity :
            Bool
        , cmdwants :
            List Text
        , cmdavoids :
            List Text
        , vars :
            List types.EnvVar
        , timeout :
            Optional Integer
        , runExitcode :
            Optional types.ExitCode
        , passvars :
            List Text
        , powercap :
            Powercap
        , runwd :
            Optional Text
        }
  
  let emptyMakeBaseArg =
          λ(cmd : Text)
        → λ(manifest : Text)
        → λ(daemonCfg : Text)
        → λ(containerName : Text)
        →   { manifest =
                manifest
            , cmd =
                cmd
            , containerName =
                containerName
            , daemonCfg =
                daemonCfg
            , args =
                [] : List Text
            , verbosity =
                types.Verbosity.Verbose
            , singularity =
                False
            , cmdwants =
                [] : List Text
            , cmdavoids =
                [] : List Text
            , vars =
                [] : List types.EnvVar
            , timeout =
                [] : Optional Integer
            , runExitcode =
                [] : Optional types.ExitCode
            , passvars =
                [] : List Text
            , powercap =
                < NoCap = {=} | Cap : Text >
            , runwd =
                [] : Optional Text
            }
          : MakeBaseArg
  
  let makeBase =
          λ(args : MakeBaseArg)
        →   { cmds =
                [   basicCommand
                    { name =
                        "nrmd"
                    , outprefix =
                        "nrmd"
                    , outwants =
                        [] : List Text
                    , outavoids =
                        [ "Traceback", "ValueError" ] : List Text
                    }
                  ⫽ { args =
                        [ args.daemonCfg ] : List Text
                    , vars =
                        [ { varname =
                              "ARGO_NODEOS_CONFIG"
                          , value =
                              "/tmp/argo_nodeos_config"
                          }
                        ]
                    }
                , let b =
                        basicCommand
                        { name =
                            "nrm"
                        , outprefix =
                            "nrmrun"
                        , outwants =
                            args.cmdwants
                        , outavoids =
                            args.cmdavoids
                        }
                  
                  in    b
                      ⫽ { args =
                              [ "run"
                              , "-s"
                              , args.containerName
                              , "--manifest=" ++ args.manifest
                              , "--"
                              , args.cmd
                              ]
                            # args.args
                        , passvars =
                            b.passvars # args.passvars
                        , vars =
                            args.vars
                        , timeout =
                            args.timeout
                        , exitcode =
                            args.runExitcode
                        , otherwd =
                            args.runwd
                        }
                ]
            , verbosity =
                types.Verbosity.Verbose
            , cleaning =
                types.Cleaning.Keep
            , workdir =
                "./_output"
            , pre =
                [] : List Text
            , post =
                [] : List Text
            }
          : types.Cfg
  
  let makeExtended =
          λ(args : MakeBaseArg)
        → λ(cmds : List types.Cmd)
        → let b = makeBase args in b ⫽ { cmds = b.cmds # cmds }
  
  let OptionalSleep = < Cmd : Text | Sleep : {} >
  
  let FilterTestArgs =
        { manifestname :
            Text
        , cmd :
            Text
        , args :
            List Text
        , vars :
            List types.EnvVar
        , passvars :
            List Text
        , cmdavoids :
            List Text
        , isTest :
            Bool
        , timeout :
            Optional Integer
        , powercap :
            Powercap
        }
  
  let emptyFilterTestArgs =
         λ(cmd : Text)
        →   { cmd =
                cmd
            , manifestname =
                "perfwrap.dhall"
            , args =
                [] : List Text
            , vars =
                [] : List types.EnvVar
            , passvars =
                [] : List Text
            , cmdavoids =
                [] : List Text
            , isTest =
                False
            , timeout =
                [] : Optional Integer
            , powercap =
                < NoCap = {=} | Cap : Text >
            }
          : FilterTestArgs
  
  let appTest =
          λ(daemonCfg : Text)
        → λ(args : FilterTestArgs)
        → let cn = "testContainer"
          
          in  makeExtended
              { manifest =
                      manifestDir
                  ++  (       if args.isTest
                        
                        then  args.manifestname
                        
                        else  "perfwrap.dhall"
                      )
              , daemonCfg =
                  daemonCfg
              , containerName =
                  cn
              , cmd =
                  args.cmd
              , args =
                  args.args
              , verbosity =
                  types.Verbosity.Verbose
              , singularity =
                  False
              , cmdwants =
                  [] : List Text
              , cmdavoids =
                  args.cmdavoids
              , vars =
                  args.vars
              , passvars =
                  args.passvars
              , timeout =
                  args.timeout
              , runwd =
                  [] : Optional Text
              , runExitcode =
                  [] : Optional types.ExitCode
              , powercap =
                  args.powercap
              }
              [ let b =
                      values.emptyCmd
                      { name =
                          "nrm"
                      , outfile =
                          "nrmlisten.out"
                      , errfile =
                          "nrmlisten.err"
                      }
                
                in    b
                    ⫽ { name = "nrm" , args =
                          [ "listen-cpd", "-j"]
                      , postchecks =
                            [ { filename =
                                  "nrmlisten.out"
                              , filecheck =
                                  { wants = [] : List Text
                                  , avoids =
                                      [] : List Text
                                  }
                              }
                            ]
                          : List types.FileCheck
                      }
              ]
  
  let ProgressAppTestArg =
        { cmd :
            Text
        , args :
            List Text
        , vars :
            List types.EnvVar
        , passvars :
            List Text
        , cmdavoids :
            List Text
        , isTest :
            Bool
        , timeout :
            Optional Integer
        , powercap :
            Powercap
        }
  
  let emptyProgressAppTestArg =
          λ(cmd : Text)
        → λ(args : List Text)
        →   { cmd =
                cmd
            , args =
                args
            , vars =
                [] : List types.EnvVar
            , passvars =
                [] : List Text
            , cmdavoids =
                [] : List Text
            , isTest =
                False
            , timeout =
                [] : Optional Integer
            , powercap =
                < NoCap = {=} | Cap : Text >
            }
          : ProgressAppTestArg
  
  let progressAppTest =
          λ(daemonCfg : Text)
        → λ(args : ProgressAppTestArg)
        → appTest
          daemonCfg
          (   emptyFilterTestArgs args.cmd
            ⫽ { args =
                  args.args
              , manifestname =
                  "basic.dhall"
              , vars =
                  args.vars
              , passvars =
                  args.passvars
              , cmdavoids =
                  args.cmdavoids
              , isTest =
                  args.isTest
              , timeout =
                  args.timeout
              , powercap =
                  args.powercap
              }
          )
  
  let mkListen =
          λ(cn : Text)
        → let b =
                values.emptyCmd
                { name =
                    "nrm"
                , outfile =
                    "listen-" ++ cn ++ ".out"
                , errfile =
                    "listen-" ++ cn ++ ".err"
                }
          
          in  b ⫽ { name ="nrm", args = [ "listen-cpd", "-j" ] }
  
  let powerexpeCmds =
          [ mkListen "testContainer" 
          ]
        : List types.Cmd
  
  let extendApp =
          λ(c : types.Cfg)
        → λ(isTest : Bool)
        → if isTest then c else c ⫽ { cmds = c.cmds # powerexpeCmds }
  
  in  { emptyMakeBaseArg =
          emptyMakeBaseArg
      , makeBase =
          makeBase
      , makeExtended =
          makeExtended
      , appTest =
          appTest
      , progressAppTest =
          progressAppTest
      , emptyFilterTestArgs =
          emptyFilterTestArgs
      , qmcPackDir =
          qmcPackDir
      , openmcDir =
          openmcDir
      , lammpsDir =
          lammpsDir
      , manifestDir =
          manifestDir
      , emptyProgressAppTestArg =
          emptyProgressAppTestArg
      , powerexpeCmds =
          powerexpeCmds
      , extendApp =
          extendApp
      }
